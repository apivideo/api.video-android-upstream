plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}
apply plugin: 'org.jetbrains.dokka'
apply from: 'maven-push.gradle'

android {
    compileSdk 33

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("environmentVariables", "INTEGRATION_TESTS_API_KEY=${System.getenv("INTEGRATION_TESTS_API_KEY")}")

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    packagingOptions {
        resources {
            excludes += "META-INF/LICENSE.md"
            excludes += "META-INF/LICENSE-notice.md"
        }
    }
}

dokkaHtml {
    moduleName.set("api.video Android $project.name library")
    suppressInheritedMembers.set(true)

    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            skipDeprecated.set(true)
            includeNonPublic.set(false)
            skipEmptyPackages.set(true)
        }
    }
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ 
                "customAssets" : [
                    "${file("$rootDir/docs/docs_logo.svg")}",
                    "${file("$rootDir/docs/logo-icon.svg")}"
                ] 
        }"""]
    )
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    api 'io.github.thibaultbee:streampack:2.4.2'
    api "video.api:android-video-uploader:1.1.1"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'video.api:android-api-client:1.2.2'
}